plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.5.11'
}

group = 'com.github.minustenchan'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "oraxen"
        url = "https://repo.oraxen.com/releases"
    }
}

dependencies {
    paperweight.paperDevBundle '1.20.4-R0.1-SNAPSHOT'
    implementation 'com.jeff-media:custom-block-data:2.2.2'
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'io.th0rgal:oraxen:1.173.0'
}

shadowJar {
    relocate 'com.jeff_media.customblockdata', group + '.customblockdata'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.named('build') {
    dependsOn 'clean'
    dependsOn 'shadowJar'
    dependsOn 'reobfJar'
}

tasks.named('shadowJar') {
    mustRunAfter 'clean'
}

tasks.named('reobfJar') {
    mustRunAfter 'shadowJar'
}

tasks.named('build') {
    mustRunAfter 'reobfJar'
}
